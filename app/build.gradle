apply plugin: 'com.android.application'
import org.apache.tools.ant.taskdefs.condition.Os

def getGitHubCommit = {
    try {
        def hashOutput = new ByteArrayOutputStream()
        def changeOutput = new ByteArrayOutputStream()
        def gitVersionName
        exec {
            commandLine 'git', 'rev-list', '--max-count=1', 'HEAD'
            standardOutput = hashOutput
        }
        exec {
            commandLine 'git', 'diff-index', '--shortstat', 'HEAD'
            standardOutput = changeOutput
        }
        gitVersionName = hashOutput.toString().trim().substring(0, 7);
        if (!changeOutput.toString().trim().empty) {
            def pattern = Pattern.compile("\\d+");
            def matcher = pattern.matcher(changeOutput.toString().trim())
            if (matcher.find()) {
                gitVersionName += "-" + matcher.group()
            }
        }
        return gitVersionName
    } catch (ignored) {
        return "UNKNOWN"
    }
}

android {
    signingConfigs {
        if (file('.signing/app-release.prop').exists()) {
            release {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-release.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
        debug {
            if (file('.signing/app-debug.prop').exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-debug.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
        fdroid {
            if (file('.signing/app-fdroid.prop').exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-fdroid.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            universalApk true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileSdkVersion 29
    //noinspection GradleDependency
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "uk.openvk.android.legacy"
        //noinspection MinSdkTooLow
        minSdkVersion 7
        //noinspection GradleDependency,OldTargetApi
        targetSdkVersion 29
        versionCode 240
        versionName "1.2.240-u"
    }
    buildTypes {
        release {
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
            buildConfigField "String", "SOURCE_CODE", "\"https://github.com/openvk/mobile-android-legacy\""
            ndk {
                abiFilters 'x86', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
            buildConfigField "String", "SOURCE_CODE", "\"https://github.com/openvk/mobile-android-legacy\""
            ndk {
                abiFilters 'x86', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    productFlavors {
        prereleaseConfig {
            applicationId "uk.openvk.android.legacy"
            //noinspection MinSdkTooLow
            minSdkVersion 7
            //noinspection OldTargetApi
            targetSdkVersion 29
            versionCode 240
            versionName "1.2.240-d"
            signingConfig signingConfigs.debug
        }
        releaseConfig {
            minSdkVersion 7
            applicationId 'uk.openvk.android.legacy'
            targetSdkVersion 29
            versionCode 240
            versionName '1.2.240'
            if (file('.signing/app-release.prop').exists()) {
                signingConfig signingConfigs.release
            }
        }
        fdroidConfig {
            applicationId "uk.openvk.android.legacy"
            //noinspection MinSdkTooLow
            minSdkVersion 7
            //noinspection OldTargetApi
            targetSdkVersion 29
            versionCode 240
            versionName "1.2.240-f"
            if (file('.signing/app-fdroid.prop').exists()) {
                signingConfig signingConfigs.fdroid
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    task mPlayerBuild(type: Exec, description: 'Compile OpenVK Media Player via NDK') {
        def ndkDir = android.ndkDirectory
        def projDir = project.projectDir.toString()
        println  "Compiling OpenVK Media Player..."
        println  "Project Directory:   ${projDir}"
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine "$ndkDir\\ndk-build.cmd",
                    "NDK_PROJECT_PATH=$projDir/app/src/main",
                    "NDK_LIBS_OUT=$projDir/app/src/main/jniLibs",
                    "APP_BUILD_SCRIPT=$projDir/../ndk-modules/ovkmplayer/Android.mk",
                    "NDK_APPLICATION_MK=$projDir/../ndk-modules/ovkmplayer/Application.mk"
        } else {
            commandLine "$ndkDir/ndk-build.sh",
                    "NDK_PROJECT_PATH=$projDir/app/src/main",
                    "NDK_LIBS_OUT=$projDir/app/src/main/jniLibs",
                    "APP_BUILD_SCRIPT=$projDir/ndk-modules/ovkmplayer/Android.mk",
                    "NDK_APPLICATION_MK=$projDir/ndk-modules/ovkmplayer/Application.mk"
        }
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn mPlayerBuild
    }
    externalNativeBuild {
        ndkBuild {
            path '../ndk-modules/ovkmplayer/Android.mk'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:multidex:1.0.3'
    compile 'com.commit451:PhotoView:1.2.5'
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:support-v4:24.0.0'
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:appcompat-v7:24.0.0'
    //noinspection GradleDependency
    compile 'com.takisoft.fix:preference-v7:24.0.0.1'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile project(':modules:actionbar')
    compile project(':modules:popupmenu')
    compile 'com.reginald.swiperefresh:library:1.1.2'
    compile 'com.seppius.plurals:android-i18n-plurals:1.1'
    // Android 2.x plurals patch
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'ch.acra:acra:4.6.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'org.apmem.tools:layouts:1.8'
    compile project(':modules:twemojicon')
    compile project(':modules:ovk-api')
}
