apply plugin: 'com.android.application'

def getGitHubCommit = {
    try {
        def hashOutput = new ByteArrayOutputStream()
        def changeOutput = new ByteArrayOutputStream()
        def gitVersionName
        exec {
            commandLine 'git', 'rev-list', '--max-count=1', 'HEAD'
            standardOutput = hashOutput
        }
        exec {
            commandLine 'git', 'diff-index', '--shortstat', 'HEAD'
            standardOutput = changeOutput
        }
        gitVersionName = hashOutput.toString().trim().substring(0, 7);
        if (!changeOutput.toString().trim().empty) {
            def pattern = Pattern.compile("\\d+");
            def matcher = pattern.matcher(changeOutput.toString().trim())
            if (matcher.find()) {
                gitVersionName += "-" + matcher.group()
            }
        }
        return gitVersionName
    } catch (ignored) {
        return "UNKNOWN";
    }
}

android {
    signingConfigs {
        if (file('.signing/app-release.prop').exists()) {
            release {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-release.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
        debug {
            if (file('.signing/app-debug.prop').exists()) {
                Properties props = new Properties();
                props.load(new FileInputStream(file(".signing/app-debug.prop")))
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
            }
        }
    }
    splits {
        abi {
            enable true
            reset()
            universalApk true
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    compileSdkVersion 29
    //noinspection GradleDependency
    buildToolsVersion "29.0.0"
    defaultConfig {
        applicationId "uk.openvk.android.legacy"
        //noinspection MinSdkTooLow
        minSdkVersion 7
        //noinspection GradleDependency,OldTargetApi
        targetSdkVersion 29
        versionCode 201
        versionName "1.1.201-beta.u"
    }
    buildTypes {
        release {
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
        debug {
            buildConfigField "String", "GITHUB_COMMIT", "\"${getGitHubCommit()}\""
            ndk {
                abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
            }
        }
    }
    productFlavors {
        prereleaseConfig {
            applicationId "uk.openvk.android.legacy"
            //noinspection MinSdkTooLow
            minSdkVersion 7
            //noinspection OldTargetApi
            targetSdkVersion 29
            versionCode 201
            versionName "1.1.201-beta"
            signingConfig signingConfigs.debug
        }
        releaseConfig {
            minSdkVersion 7
            applicationId 'uk.openvk.android.legacy'
            targetSdkVersion 29
            versionCode 201
            versionName '1.1.201-beta.r'
            if (file('.signing/app-release.prop').exists()) {
                signingConfig signingConfigs.release
            }
        }
        fdroidConfig {
            applicationId "uk.openvk.android.legacy"
            //noinspection MinSdkTooLow
            minSdkVersion 7
            //noinspection OldTargetApi
            targetSdkVersion 29
            versionCode 201
            versionName "1.1.201-beta.f"
            if (file('.signing/app-release.prop').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    externalNativeBuild {
        ndkBuild {
            path '../ndk-modules/ovkmplayer/Android.mk'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:recyclerview-v7:24.0.0'
    //noinspection GradleDependency
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.commit451:PhotoView:1.2.5'
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:support-v4:24.0.0'
    //noinspection GradleDependency,GradleCompatible
    compile 'com.android.support:appcompat-v7:24.0.0'
    //noinspection GradleDependency
    compile 'com.takisoft.fix:preference-v7:24.0.0.1'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile project(':modules:actionbar')
    compile project(':modules:popupmenu')
    compile 'com.reginald.swiperefresh:library:1.1.2'
    // Android 2.x plurals patch
    compile 'com.seppius.plurals:android-i18n-plurals:1.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'ch.acra:acra:4.6.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile project(':modules:twemojicon')
    compile project(':modules:wrhttp')
}
